# library for cpp-mini-interpreter

(set cons (fun (a b) (fun (f) (f a b))))
(set car (fun (l) (l (fun (a b) a))))
(set cdr (fun (l) (l (fun (a b) b))))

(set true 1)
(set false 0)

(set not (fun (x) (if x false true)))
(set and (fun (x y) (if x y false)))
(set or (fun (x y) (if x true y)))
(set xor (fun (x y) (if x (not y) y)))

(set neq (fun (x y) (not (eq x y))))

(set fold (fun (f init l)
  (if (eq l null) init
      (f (car l) (fold f init (cdr l))))
))
(set sum (fun (l) (fold add 0 l)))

(set map (fun (f l)
  (if (eq l null) null
      (cons (f (car l)) (map f (cdr l))))
))

(set toStr_ toStr)
(set toStr (fun (x)
  (set type (type x))
  (if (eq type "STRING") x
      (if (eq type "NULL") "null"
          (if (eq type "DEFINED_FUNCTION") "<function>"
              (if (eq type "BUILD_IN_FUNCTION") "<build_in>"
                  (toStr_ x)))))
))

(set listToStr (fun (l)
  (if (eq l null) "null"
      (concat "(" (concat (toStr (car l)) (concat " " (concat (listToStr (cdr l)) ")")))))
))

(set minus1 (sub 0 1))

# (substr "abcd" 1 3) => "bc"
(set substr (fun (s start end)
  (if (eq start end) ""
      (concat (getAt s start) (substr s (add start 1) end)))
))

# (searchChar "abc" "x") => -1
# (searchChar "abc" "c") => 2
(set searchChar (fun (s c)
  (set len (length s))
  (set searchChar_ (fun (s c i)
    (if (eq i len) minus1
        (if (eq (getAt s i) c) i
            (searchChar_ s c (add i 1))))
  ))
  (searchChar_ s c 0)
))

(set split2 (fun (c s)
  (set i (searchChar s c))
  (if (eq i minus1) (cons s "")
      (cons (substr s 0 i) (substr s (add i 1) (length s))))
))
(set split (fun (c s)
  (set i (searchChar s c))
  (if (eq i minus1) (cons s null)
      (cons (substr s 0 i)
            (split c (substr s (add i 1) (length s)))))
))
